"" Line Feeder 3
"" by David B. Dahl (some personalized edits by Mickey Warner)
"" December 2010


" When in a tmux session, with your file being editted
" by vi, hit Ctrl + k to split the window and run a
" new process (such as R for .R files or MATLAB for .m
" files). Lines can be sent from the upper window (the
" script) to the lower window (console) with Ctrl + j.

" Run every line in a script with Ctrl + H. (so far
" only compatible with R)

function! LineFeederNextLine()
    call system("tmux select-pane -D")
    call system("tmux send-keys " . shellescape(getline('.') . ""))
    call system("tmux select-pane -U")
    normal j
endfunction

function! LineFeederSetUp()
    " expand("%:t") returns the name of the file being edited, omitting the directory
    let file = expand("%:t")
    " get the file extension of current file
    let ext = expand("%:e")
    if ext == "R"
        let cmd = "nice -5 R --silent --no-restore"
    elseif ext == "m"
        let cmd = "matlab -nosplash -nodesktop"
    elseif ext == "c"
        " the '.' is vim's string concatenation operator
        " check if readable file exists
        if filereadable("./cbuild")
            let cmd = "vulture " . file . " ./cbuild"
        else
            let cmd = "vulture " . file . " cbuild " . file . " " . expand("%:r") . ".out"
        endif
    elseif ext == "py"
        let cmd = "nice -5 python"
    elseif ext == "jl"
        let cmd = "nice -5 julia"
    elseif ext == "tex"
        " Run texbuild when any .tex file in DIR is modified
        " (useful when working with multiple files that link to one tex document)
        let regex = expand("*.tex")
        let regex = substitute(regex, ".tex\n", ".tex|", "gc")
        " my texbuild function still assumes that main.tex is the file to be compiled, not
        " necessarily file
        let cmd = "vulture \"" . regex . "\" texbuild --noevince"
    elseif ext == "ly"
        let cmd = "vulture " . file . " lilypond " . file
    else
        let cmd = "bash"
    endif
    let cmd = input("Execute: ",cmd)
    if ext == "tex"
        call system("tmux split-window -d -l 2 " . shellescape("exec `while [ ! -f output_latex.pdf ]; do sleep 1; done; evince output_latex.pdf`"))
        call system("tmux split-window -d -p 30 " . shellescape("exec " . cmd))
    else
        call system("tmux split-window -d -p 34 " . shellescape("exec " . cmd))
    endif
endfunction

function! Sourcer()
    let file = expand("%:t")
    let ext = expand("%:e")
    if ext == "R"
        call system("tmux select-pane -D")
        call system("tmux send-keys 'source(\"./" . file . "\")'")
        call system("tmux select-pane -U")
    endif
endfunction

nnoremap <silent> <C-k> :call LineFeederSetUp()<cr>
nnoremap <silent> <C-j> :call LineFeederNextLine()<cr>
nnoremap <silent> <C-h> :call Sourcer()<cr>
vmap <silent> <C-j> :call LineFeederNextLine()<cr>

